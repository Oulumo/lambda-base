import org.apache.tools.ant.filters.ReplaceTokens

repositories {
    maven {
        url "http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release/"
    }
}

dependencies {
    compile project(':lambda-base-apigateway')
    compile project(':lambda-base-router-regex')

    // Dagger-2
    compile group: 'com.google.dagger', name: 'dagger', version: daggerVersion
    apt group: 'com.google.dagger', name: 'dagger-compiler', version: daggerVersion

    testCompile project(':lambda-base-tester')
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    testAptCompile group: 'com.google.dagger', name: 'dagger-compiler', version: daggerVersion
}

processResources {
    filesMatching('**/logback.xml') {
        filter {
            it.replace('${logback.loglevel}', logbackLoglevel)
        }
    }
    filesMatching('**/buildinfo.properties') {
        filter ReplaceTokens, tokens: [
                version: project.version, date: getDate()
        ]
    }

}

task buildLambdaZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}

build.dependsOn buildLambdaZip

task copyNativeDeps(type: Copy) {
    from (configurations.testCompile) {
        include "*.dylib"
        include "*.so"
        include "*.dll"
    }
    into 'build/libs'
}

test.dependsOn copyNativeDeps

test.doFirst {
    systemProperty "java.library.path", 'build/libs'
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('HH:mm MMMM d, YYYY')
    return formattedDate
}